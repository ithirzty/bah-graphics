#import "./libs/png.bah"

#import "iostream.bah"

#import "./colors.bah"


struct image {
    path: str
    width: int
    height: int
    buf: []rgbColor

    load(s str) bool {
        fs = fileStream{}
        fs.open(s, "rb")

        if fs.isValid() == false {
            return false
        }

        header uint
        fs.readPtr(&header, 8)
        if png_sig_cmp(&header, 0, 8) {
            fs.close()
            return false
        }

        pngPtr = png_create_read_struct("1.6.40", null, null, null)
        if pngPtr == null {
            fs.close()
            png_destroy_read_struct(pngPtr, null, null)
            return false
        }

        info = png_create_info_struct(pngPtr)

        if info == null {
            fs.close()
            png_destroy_read_struct(pngPtr, null, null)
            return false
        }

        png_init_io(pngPtr, fs.handle)
        png_set_sig_bytes(pngPtr, 8)

        png_read_info(pngPtr, info)

        this.width      = png_get_image_width(pngPtr, info)
        this.height     = png_get_image_height(pngPtr, info)
        color_type = png_get_color_type(pngPtr, info)
        bit_depth  = png_get_bit_depth(pngPtr, info)

        if bit_depth == 16 {
            png_set_strip_16(pngPtr)
        }

        if color_type == PNG_COLOR_TYPE_PALETTE {
            png_set_palette_to_rgb(pngPtr)
        }

        if color_type == PNG_COLOR_TYPE_GRAY && bit_depth < 8 {
            png_set_expand_gray_1_2_4_to_8(pngPtr)
        }

        if color_type & PNG_COLOR_MASK_ALPHA == 0 {
            png_set_add_alpha(pngPtr, 0xFF, 1)
        }

        if color_type == PNG_COLOR_TYPE_GRAY || color_type == PNG_COLOR_TYPE_GRAY_ALPHA {
            png_set_gray_to_rgb(pngPtr)
        }

        png_read_update_info(pngPtr, info)

        allocateArray(this.buf, this.width * this.height)

        row_pointers = []ptr

        i=0; for i < this.height, i++ {
            row_pointers[i] = <ptr>(<uint>cArr(this.buf) + i * this.width * 4)
        }

        png_read_image(pngPtr, cArr(row_pointers))

        png_destroy_read_struct(pngPtr, null, null)

        fs.close()

        return true
    }
}