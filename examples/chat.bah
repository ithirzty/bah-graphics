#!/bin/bah -linkAndRun

//This example illustrates a simple user interface of a texting app.
//To do so, the screen is split in two as two root elements:
//one for conaining the messages and one for the text input.

#import "iostream.bah"

#import "../ui.bah"


msgContainer uiElement* //The element containg the messages.

//Generates an uiElement from a message.
generateMessage(s str, author str) uiBox* {
    xPos = pixels(0)

    if author == "You" {
        //Put the right end of the element at 100% of its parent width.
        //This results in the element appearing on the right side of the screen.
        xPos = percents(100).basis(uiBasisEnd)
    }

    return new uiBox {
        padding: [10,10,10,10]
        pos: [
            xPos, pixels(0)
        ]
        backdrop: function(bx uiBox*, w ui*) {
            bx.size = bx.getInnerSize()
            //Changes the background color depending on the author.
            color = rgbColor{0, 200, 80}
            if ((<uiText*>bx.elements[0].elements[1]).text) != "You" {
                color = rgbColor{0, 180, 200}
            }
            w.drawRoundedRect(bx.getPosition(), bx.size, 8, color)
        }

        elements: []uiElement* {
            new uiVerticalArray { //There are two text element one on top of the other
                spacing: 10
                elements: []uiElement* {
                    new uiText { //The message body
                        text: s
                    },
                    new uiText { //The message author written in the bottom right corner
                        text: author
                        focusable: false //This should not be focusable as there is no useful text to select.
                        color: WHITE
                        pos: [
                            percents(100).basis(uiBasisEnd),
                            pixels(0)
                        ]

                    }
                }
            }
        }
    }
}

main(args []str) int {
    ui = ui{}

    //The messages container
    ui.addElement(
        new uiBox { //Using a box as it is scrollable.
            backdrop: function(bx uiBox*, ui ui*) {
                //Make sure its makes the full width of the window but 50 pixels short in height for the input below.
                bx.size = [ui.width, ui.height - 50]
                pos = bx.getPosition()

                bx.elements[0].size[0] = bx.size[0] //sets the width of the messages vertical array

                ui.drawRect(pos, bx.size, rgbColor{220, 220, 220})
            }

            elements: []uiElement* {
                new uiVerticalArray { //the vertical array container
                    id: "msgContainer" //sets an id to find it easily -> msgContainer = ui.find("msgContainer")
                    dynamicSize: false //As we set its width (line 73) we do not want it to grow on that axis
                    padding: [10, 10, 10, 10]
                    spacing: 20
                    elements: []uiElement* {
                        generateMessage("Hi, how are you?", "Alois") // Generate a first message
                    }
                }
            }
        }
    )

    msgContainer = ui.find("msgContainer")

    //The text input box.
    ui.addElement(
        new uiBox {
            pos: [
                pixels(0), percents(100).basis(uiBasisEnd) //bottom of the screen
            ]
            size: [
                ui.width,
                50
            ]
            backdrop: function(bx uiBox*, ui ui*) {
                bx.size[0] = ui.width //resize it on every draw in case the window size changed
                bx.elements[0].size[0] = ui.width - (30 + bx.elements[1].size[0]) //set the width of the text input element
            }

            elements: []uiElement* {
                new uiTextInput {
                    pos: [
                        pixels(10),
                        pixels(10)
                    ]
                    text: "Text message"
                    onsubmit: function(inp uiTextInput*) {
                        btn = (<uiButton*>inp.parent.elements[1])
                        btn.onclick() //call the btn.onclick callback instead of duplicating code
                    }
                },
                new uiButton {
                    pos: [
                        percents(100).basis(uiBasisEnd),
                        pixels(10)
                    ]

                    margin: [
                        pixels(-10), pixels(0) //shift it 10px left
                    ]

                    text: "Send"

                    onclick: function(btn uiButton*) { //when clicked
                        ui = <ui*>btn.window
                        inp = <uiTextInput*>btn.parent.elements[0]

                        if len(inp.value) == 0 {
                            return
                        }

                        shouldScroll = false
                        if msgContainer.parent.offsets[1] >= msgContainer.size[1] - msgContainer.parent.size[1] {
                            shouldScroll = true
                        }

                        //Add messages to the message container
                        msgContainer.addElement(generateMessage(arrToStr(inp.value), "You"))
                        msgContainer.addElement(generateMessage("This is interesting.", "Alois"))

                        if shouldScroll {
                            //Scroll the message continer if necessary
                            msgContainer.parent.offsets[1] = msgContainer.size[1] - msgContainer.parent.size[1]
                            if msgContainer.parent.offsets[1] < 0 {
                                msgContainer.parent.offsets[1] = 0
                            }
                            msgContainer.redraw()
                        }
                        
                        //Clear the input value
                        clear(inp.value)
                        inp.redraw()
                    }
                }
            }
        }
    )
    
    ui.launch(900, 700, "Chat UI test")

    return 0
}