#import "../ui_struct.bah"

#import "./base.bah"

BUTTON_BACKGROUND_COLOR = rgbColor{220, 220, 220}
BUTTON_BACKGROUND_HOVERED_COLOR = rgbColor{191, 191, 255}
BUTTON_BACKGROUND_CLICKED_COLOR = rgbColor{120, 120, 255}
BUTTON_TEXT_COLOR = rgbColor{0,0,0}
BUTTON_CLICKED_TEXT_COLOR = rgbColor{255,255,255}

struct uiButton extend uiElement {
    text: str = "Button"
    onclick: function(uiButton*)
    showTime: uint
    bgColor: rgbColor
    fgColor: rgbColor
    radius: int = -1

    events(evnt uint32) {
        clickFn = this.onclick

        if evnt == uiEventElementHoverIn || evnt == uiEventElementHoverOut {
            this.showTime = getTimeUnix()
        }

        if evnt == uiEventElementClicked {
            if clickFn != null {
                clickFn(this)
            }
        } else if evnt == windowEventKeyDown {
            c = this.window.getEventChar()
            if c == Char_enter || c == <uint32>' ' {
                if clickFn != null {
                    clickFn(this)
                }
            } else if c == Char_tab || c == Char_shit_tab {
                ui = <ui*>this.window
                i=0; for i < len(ui.elements), i++ {
                    if ui.elements[i] == this {
                        if this.window.isKeyModifier(Key_SHIFT) {
                            i--
                            for i != -1, i-- {
                                if ui.elements[i].focusable {
                                    ui.setFocus(ui.elements[i])
                                    break
                                }
                            }
                            break
                        } else {
                            i++
                            for i < len(ui.elements), i++ {
                                if ui.elements[i].focusable {
                                    ui.setFocus(ui.elements[i])
                                    break
                                }
                            }
                            break
                        }
                    }
                }
            }
        }

    }

    draw(w ui*) {
        now = getTimeUnix()
        w.font.setSize(this.fontSize)
        textWidth = w.measureText(w.font, this.text)[0]

        this.size[0] = pixels(textWidth + this.padding[0] * 2)
        this.size[1] = pixels(this.fontSize + this.padding[1] * 2)

        pos = this.getPosition()
        size = this.getSize()

        bgColor = this.bgColor
        fgColor = this.fgColor

        if this.clicked {
            this.bgColor = BUTTON_BACKGROUND_CLICKED_COLOR
            this.fgColor = BUTTON_CLICKED_TEXT_COLOR
            bgColor = this.bgColor
            fgColor = this.fgColor
        } else {
            if now - this.showTime < 120000000 {
                amm = ((now - this.showTime) * 255) / 120000000
                if this.hovered {
                    bgColor = this.bgColor.interpolate(BUTTON_BACKGROUND_HOVERED_COLOR, amm)
                    fgColor = this.fgColor.interpolate(BUTTON_TEXT_COLOR, amm)
                } else {
                    bgColor = this.bgColor.interpolate(BUTTON_BACKGROUND_COLOR, amm)
                    fgColor = this.fgColor.interpolate(BUTTON_TEXT_COLOR, amm)
                }
                this.redrawIn(40)
            } else {
                if this.hovered {
                    this.bgColor = BUTTON_BACKGROUND_HOVERED_COLOR
                    this.fgColor = BUTTON_TEXT_COLOR
                } else {
                    this.bgColor = BUTTON_BACKGROUND_COLOR
                    this.fgColor = BUTTON_TEXT_COLOR
                }
                bgColor = this.bgColor
                fgColor = this.fgColor
            }
        }

        if this.radius == 0 {
            w.drawRect(pos, size, bgColor)
        } else {
            w.drawRoundedRect(pos, size, this.radius, bgColor)
        }
        w.drawText(w.font, this.text, fgColor, [pos[0] + this.padding[0], pos[1] + size[1] / 2 + this.fontSize / 2 + 2])
    }

    _init() {
        this._events = this.events
        this._draw = this.draw

        this.bgColor = BUTTON_BACKGROUND_COLOR
        this.fgColor = BUTTON_TEXT_COLOR
        
        this.cursor = "hand1"

        if this.fontSize == 0 {
            this.fontSize = 8
        }

        if this.radius == -1 {
            this.radius = 8
        }

        if this.padding[0] == 0 {
            this.padding = [12, 12, 12, 12]
        }

    }
}